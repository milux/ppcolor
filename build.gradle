buildscript {
    repositories {
        mavenCentral()
        flatDir dirs: "C:\\proguard\\lib"
    }
    dependencies {
        classpath ":proguard:"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
    id "com.github.breadmoirai.github-release" version "2.2.0"
}

group 'de.milux.ppcolor'
version '1.0.2'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'de.milux.ppcolor.MainKt'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task proguard(type: proguard.gradle.ProGuardTask) {
    configuration file('proguard.txt')
    injars "build/libs/de.milux.ppcolor-${version}.jar"
    outjars "releases/ppcolor-${version}.jar"
}
proguard.dependsOn jar

githubRelease {
    token getProperty('github.token')
    owner "milux"
    repo "ppcolor"
    tagName "${version}"
    releaseAssets "releases/ppcolor-${version}.jar"
}